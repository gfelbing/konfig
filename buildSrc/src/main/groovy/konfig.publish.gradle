plugins {
    id 'maven-publish'
    id 'signing'
}

def githubUrl = 'https://github.com/gfelbing/konfig'
group = "io.github.gfelbing"
version = "0.4.1-SNAPSHOT"
def isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
    withJavadocJar()
    withSourcesJar()
}


publishing {
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.properties['ossrh.username']
                password = project.properties['ossrh.password']          
            }
        }
    }
    publications {
        mavenCentral(MavenPublication) {
            pom {
                groupId = 'io.github.gfelbing'
                name = project.name
                description = 'Lightweight configuration framework for kotlin'
                url = githubUrl
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/gfelbing/konfig.git'
                    developerConnection = 'scm:git:ssh://github.com/gfelbing/konfig.git'
                    url = githubUrl
                }
                developers {
                    developer {
                        id = 'gfelbing'
                        name = 'Georg Welzel'
                        email = 'gwelzel@mailbox.org'
                    }
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenCentral
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
